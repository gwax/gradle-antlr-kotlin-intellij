buildscript {
    ext.antlr4_version = '4.7'
    ext.kotlin_version = '1.2.0'
    ext.junit4_version = '4.12'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "antlr"

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:$antlr4_version"
    compile "org.antlr:antlr4-runtime:$antlr4_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "junit:junit:$junit4_version"
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}
compileKotlin.dependsOn generateGrammarSource

sourceSets.main.java.srcDir file("$buildDir/generated-src/antlr/main/")

idea {
    module {
        generatedSourceDirs += file("$buildDir/generated-src/antlr/main/")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

